plugins {
  id 'maven-publish'
  id 'java-library'
  id 'signing'
  id('io.github.gradle-nexus.publish-plugin') version '1.1.0'
}

group = 'io.camunda'
version = '1.3.6'
sourceCompatibility = '11'

repositories {
  mavenCentral()
}

dependencies {
  api "io.camunda:thymeleaf-feel:1.1.1"
  implementation 'com.sun.mail:jakarta.mail:1.6.7'
  implementation 'jakarta.activation:jakarta.activation-api:2.1.2'
  api "com.google.api-client:google-api-client:1.34.1"
  api "com.google.oauth-client:google-oauth-client-jetty:1.33.3"
  api "com.google.apis:google-api-services-sheets:v4-rev20220411-1.32.1"
  api "com.google.apis:google-api-services-drive:v3-rev20220417-1.32.1"
  api "com.google.apis:google-api-services-gmail:v1-rev110-1.25.0"

  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

tasks.named('test') {
  useJUnitPlatform()
}

task sourceJar(type: Jar) {
  outputs.cacheIf { true }
  from sourceSets.main.allJava
  archiveClassifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  from javadoc.destinationDir
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  archiveClassifier = 'javadoc'
}
java {
  withSourcesJar()
  withJavadocJar()
}

nexusPublishing {
  transitionCheckOptions {
    maxRetries.set(100)
    delayBetween.set(Duration.ofSeconds(10))
  }
  repositories {
    sonatype {
	  stagingProfileId = "13992b29744076"
	  nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
	  snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
	}
  }
}

signing {
    useInMemoryPgpKeys(System.getenv("ORG_GRADLE_PROJECT_signingKey"), System.getenv("ORG_GRADLE_PROJECT_signingPassword"))
    sign(publishing.publications)
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from(components.java)
      versionMapping {
         usage('java-api') {
             fromResolutionResult()
         }
      }
      pom {
        groupId = 'io.camunda'
        artifactId = 'camunda-google-ws-java'
        packaging = 'jar'
        name = 'Camunda Google Workspace Utilities'
        description = "Some utilities around Google WS to be easily plugged in a Camunda 8 project (or anywhere else)"
        url = "https://github.com/camunda-community-hub/camunda-google-ws-java"
        licenses {
          license {
           name = "The Apache License, Version 2.0"
           url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
          }
        }
        developers {
          developer {
            name = "christophe.dame"
            email = "christophe.dame@camunda.com"
          }
        }
        scm {
          connection = "scm:git:git@github.com:camunda-community-hub/camunda-google-ws-java.git"
          developerConnection = "scm:git:git@github.com:camunda-community-hub/camunda-google-ws-java.git"
          url = "https://github.com/camunda-community-hub/camunda-google-ws-java"
        }
      }
    }
  }
}

